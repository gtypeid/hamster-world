plugins {
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.jetbrains.kotlin.jvm' version '2.1.0' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '2.1.0' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.1.0' apply false
    id 'org.jetbrains.kotlin.kapt' version '2.1.0' apply false
}

allprojects {
    group = 'com.hamsterworld'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
    apply plugin: 'org.jetbrains.kotlin.kapt'

    // common 모듈은 실행 가능한 jar가 아님
    if (project.name == 'common') {
        bootJar.enabled = false
        jar.enabled = true
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += ["-Xjsr305=strict"]
            jvmTarget = "21"
        }
    }

    dependencies {
        // Kotlin 공통
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

        // Spring Boot 공통
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // MySQL
        runtimeOnly 'com.mysql:mysql-connector-j:8.0.33'

        // QueryDSL
        implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
        kapt 'com.querydsl:querydsl-apt:5.1.0:jakarta'
        kapt 'jakarta.annotation:jakarta.annotation-api:3.0.0'
        kapt 'jakarta.persistence:jakarta.persistence-api:3.2.0'

        // Test 공통
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly 'com.h2database:h2'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // QueryDSL 생성 경로 설정
    sourceSets {
        main {
            kotlin {
                srcDirs = ["src/main/kotlin", "build/generated/source/kapt/main"]
            }
        }
    }
}
